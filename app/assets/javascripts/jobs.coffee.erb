# Place all the behaviors and hooks related to the matching controller here.
# All this logic will automatically be available in application.js.
# You can use CoffeeScript in this file: http://coffeescript.org/

# SOURCE: http://stackoverflow.com/a/32358641
<% self.class.include Rails.application.routes.url_helpers %>

# $(document).on 'ready page:load', (($$, $)->
# SOURCE: http://brandonhilkert.com/blog/organizing-javascript-in-rails-application-with-turbolinks/
$(document).on 'page:change', ->
  do ( $$ = window.Basimilch, $ = jQuery ) ->
    $('#job_job_type_id').change ->
      $('body').css 'cursor', 'wait'
      location.href = "<%= new_job_path %>" + "?job_type=" + $(@).val()
    $$.setupFilterAction 'job_type_filter_selector', "<%= jobs_path %>", "job_type"
    $$.setupFilterAction 'job_month_filter_selector', "<%= jobs_path %>", "month"
    if $$.isAdminPage()
      # DOC: https://github.com/twitter/typeahead.js/tree/v0.11.1
      # DOC: https://twitter.github.io/typeahead.js/examples/
      users = new Bloodhound
        datumTokenizer: (user) ->
          firstNameTokens = Bloodhound.tokenizers.whitespace(user.first_name);
          lastNameTokens  = Bloodhound.tokenizers.whitespace(user.last_name);
          emailTokens     = Bloodhound.tokenizers.whitespace(user.email);
          idTokens        = Bloodhound.tokenizers.whitespace(user.id);
          firstNameTokens.concat(lastNameTokens).concat(emailTokens).concat(idTokens);
        queryTokenizer: Bloodhound.tokenizers.whitespace
        prefetch: '/users.json'
        remote:
          url: '/users.json?q=%QUERY'
          wildcard: '%QUERY'
      users.initialize()
      # DOC: https://github.com/bootstrap-tagsinput/bootstrap-tagsinput/tree/0.8.0
      # DOC: http://bootstrap-tagsinput.github.io/bootstrap-tagsinput/examples/
      usersInput = $('#users')
      usersInput.tagsinput
        itemValue: 'id'
        freeInput: false
        trimValue: true
        maxTags: usersInput.data('available-slots')
        itemText: (user) -> user.first_name + " " + user.last_name
        typeaheadjs:[
            hint: false
            highlight: true
          ,
            name: 'users'
            display: (user) -> user.first_name + " " + user.last_name
            source: users.ttAdapter()
            templates:
              pending: [
                '<div class="tt-pending-message">',
                  '<span class="glyphicon glyphicon-refresh" aria-hidden="true"></span>'
                '</div>'
              ].join('\n')
              empty: [
                '<div class="tt-empty-message">',
                  usersInput.data('typeahead-no-suggestions-msg'),
                '</div>'
              ].join('\n')
              suggestion: (user) ->
                [
                  '<div><div>'
                  user.first_name
                  user.last_name
                  '(' + user.id + ')'
                  '</div><div><small><i>'
                  user.email
                  '</i></small></div></div>'
                ].join('\n')
        ]
      submitBtn = $('.signup-other-users input[type=submit]')
      usersInput.on 'itemAdded itemRemoved', (e) ->
        # e.item: contains the item
        submitBtn.prop 'disabled', not usersInput.val()

